apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'dagger.hilt.android.plugin'
apply from: "../constants.gradle"

android {
  compileSdkVersion config.compileSdkVersion
  defaultConfig {
    applicationId config.packageName
    minSdkVersion config.minSdkVersion
    targetSdkVersion config.targetSdkVersion
    versionCode config.versionCode
    versionName config.versionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    buildConfigField "String", "API_KEY", "\"" + properties.getProperty("api_token") + "\""
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles(getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro")
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    coreLibraryDesugaringEnabled true
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  kapt {
    correctErrorTypes true
  }
  viewBinding {
    enabled = true
  }
}

dependencies {

  // desugar
  coreLibraryDesugaring(Dep.desugar)

  // kotlin
  implementation(Dep.kotlin)

  // coroutine
  implementation(Dep.coroutineCore)
  implementation(Dep.coroutineAndroid)
  testImplementation(Dep.coroutineTest)

  // androidx
  implementation(Dep.appCompat)
  implementation(Dep.material)
  implementation(Dep.constraintLayout)
  implementation(Dep.activityKtx)
  implementation(Dep.fragmentKtx)

  // lifecycle
  implementation(Dep.lifecycleRuntime)
  implementation(Dep.lifecycleExtensions)
  kapt(Dep.lifecycleCompiler)
  implementation(Dep.viewmodelKtx)
  implementation(Dep.livedataKtx)


  // navigation
  implementation(Dep.navCommonKtx)
  implementation(Dep.navRuntimeKtx)
  implementation(Dep.navFragmentKtx)
  implementation(Dep.navUiKtx)

  // dependency injection
  implementation(Dep.dagger)
  kapt(Dep.daggerCompiler)
  implementation(Dep.daggerHilt)
  kapt(Dep.daggerHiltCompiler)
  implementation(Dep.androidHilt)
  implementation(Dep.androidHiltViewModel)
  kapt(Dep.androidHiltCompiler)

  // network
  implementation(Dep.retrofit)
  implementation(Dep.retrofitConverter)
  debugImplementation(Dep.loggingInterceptor)
  testImplementation(Dep.mockWebServer)

  // database
  implementation(Dep.roomRuntime)
  kapt(Dep.roomCompiler)
  implementation(Dep.roomKtx)

  // recycler view
  implementation(Dep.groupie)
  implementation(Dep.groupieViewBinding)

  // ui
  implementation(Dep.circleImage)

  // image
  implementation(Dep.picasso)

  // test
  testImplementation(Dep.junit)
  testImplementation(Dep.truth)
  testImplementation(Dep.testCore)
  testImplementation(Dep.mockk)
}